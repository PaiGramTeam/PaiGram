xiao char lvl=90/90 cons=0 talent=9,9,9;
xiao add weapon="deathmatch" refine=1 lvl=90/90;
xiao add set="vermillionhereafter" count=4;
xiao add stats hp=4780 atk=311 atk%=0.466 anemo%=0.466 cd=0.622;
xiao add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.2755 em=39.64 cr=0.3641 cd=0.662;

lisa char lvl=90/90 cons=6 talent=9,9,9;
lisa add weapon="favoniuscodex" refine=3 lvl=90/90;
lisa add set="no" count=4;
lisa add stats hp=4780 atk=311 er=0.518 electro%=0.466 cr=0.311;
lisa add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=39.64 cr=0.2979 cd=0.5958;

venti char lvl=90/90 cons=0 talent=9,9,9;
venti add weapon="alleyhunter" refine=1 +params=[stacks=10] lvl=90/90;
venti add set="emblemofseveredfate" count=4;
venti add stats hp=4780 atk=311 atk%=0.466 anemo%=0.466 cr=0.311;
venti add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

faruzan char lvl=90/90 cons=6 talent=9,9,9;
faruzan add weapon="favoniuswarbow" refine=4 lvl=90/90;
faruzan add set="tom" count=4;
faruzan add stats hp=4780 atk=311 atk%=0.466 anemo%=0.466 cr=0.311;
faruzan add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.2204 em=39.64 cr=0.331 cd=0.7944;

options swap_delay=12 debug=true;
target lvl=100 resist=0.1 radius=2 pos=0,2.4  hp=10000000;
energy every interval=480,720 amount=1; 
active lisa;

for let i = 0; i < 4; i = i + 1 {
  lisa attack, skill[hold=1], attack, burst;
  while !.lisa.mods.favonius-cd { 
    lisa attack;
  } #proc fav
  faruzan burst;
  while !.faruzan.mods.favonius-cd { 
    faruzan attack;
  }  #proc fav
  venti skill, burst;
  xiao skill:2, burst;
  for let h = 0; h < 11; h = h + 1 {
    let col = rand();
    if col > 0.5 {
      xiao jump, high_plunge[collision=1];
    } else {
      xiao jump, high_plunge;
    }

  }
  for let h = 0; h < 2; h = h + 1 {
    let col = rand();
    if col > 0.5 {
      xiao jump, low_plunge[collision=1];
    } else {
      xiao jump, low_plunge;
    }

  }
  
}