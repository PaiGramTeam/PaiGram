raiden char lvl=90/90 cons=0 talent=9,9,9;
raiden add weapon="dragonsbane" refine=3 lvl=90/90;
raiden add set="fopl" count=5;
raiden add stats hp=4780 atk=311 em=187 em=187 em=187; #main
raiden add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1102 em=118.92 cr=0.3972 cd=0.5296;

fischl char lvl=90/90 cons=6 talent=9,9,9;
fischl add weapon="thestringless" refine=3 lvl=90/90;
fischl add set="gt" count=5;
fischl add stats hp=4780 atk=311 em=187 electro%=0.466 cr=0.311 ; #main
fischl add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1488 er=0.1102 em=59.46 cr=0.331 cd=0.7944;

xingqiu char lvl=90/90 cons=6 talent=9,9,9;
xingqiu add weapon="wolffang" refine=1 lvl=90/90;
xingqiu add set="emblemofseveredfate" count=4;
xingqiu add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cd=0.622 ; #main
xingqiu add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.3857 em=39.64 cr=0.3972 cd=0.4634;

nahida char lvl=90/90 cons=0 talent=9,9,9;
nahida add weapon="thewidsith" refine=3 lvl=90/90;
nahida add set="deepwoodmemories" count=4;
nahida add stats hp=4780 atk=311 em=187 dendro%=0.466 cr=0.311 ; #main
nahida add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1102 em=79.28 cr=0.331 cd=0.7944;


options swap_delay=12 iteration=1000;
target lvl=100 resist=0.1 radius=3 pos=0,3.5 hp=999999999; 
energy every interval=480,720 amount=1;

active nahida;

    nahida skill;
    
for let i=0; i<4; i=i+1 {

    nahida burst;
    if i != 0 {
      nahida attack, charge, dash, charge;
    }
    
    raiden skill;
    if i != 0 {
      raiden attack;
    }
    
    fischl skill, attack;
    
    xingqiu burst, attack;
    
    nahida attack:2, skill,
           attack:3, charge,
           attack:3, dash,
           attack:3, dash,
           attack:2, charge;

    fischl attack:2, burst;

    xingqiu attack, skill, dash, attack:2;
    nahida attack:2, skill,
           attack:2, dash,
           attack:3, dash,
           attack:2, dash,
           attack:3;
           
    print("done");
}

wait(12);

# 6% of the field time controls 49% of the dps

# changelog
#   - stat solver: hessey
# - initial sim: vigne
# - 4tf 23.5s rotation: lettuce hunt
# - raiden E opener, N2/N3 optimization: skippi
# - lionsroar xingqiu: xardas (90k record)
# - sim wait() cutoff: skippi
# - wait(12) fischl burst: charlie
# - N1Q fischl, raiden before fischl: lettuce hunt
# - fixed rotation with gt fischl and wolffang xingqiu ocram2012 (94.5k)