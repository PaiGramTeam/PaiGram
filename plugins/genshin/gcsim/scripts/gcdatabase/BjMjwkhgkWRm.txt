# --- Pre action hook setup ---

fn rand_delay(mean, stddev) {
  let del = randnorm() * stddev + mean;
  if del > (mean + mean) {
    del = mean + mean;
  }
  delay(del);
}

let prev_char_id = -1;
let prev_action_id = -1;
let _execute_action = execute_action;

fn execute_action(char_id number, action_id number, p map) {
  if prev_char_id != char_id && action_id != .action.swap {
    execute_action(char_id, .action.swap, []);
  }

  if action_id == .action.swap {
    # add delay before swap
    rand_delay(12, 3);
  }

  prev_char_id = char_id;
  prev_action_id = action_id;
  return _execute_action(char_id, action_id, p);
}

# --- Team
alhaitham char lvl=90/90 cons=0 talent=9,9,9;
alhaitham add weapon="harbingerofdawn" refine=5 lvl=90/90;
alhaitham add set="gd" count=5;
alhaitham add stats hp=4780 atk=311 em=187 dendro%=0.466 cr=0.311 ; #main
alhaitham add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1653 em=59.46 cr=0.331 cd=0.7944; #substats

raiden char lvl=90/90 cons=0 talent=9,9,9;
raiden add weapon="dragonsbane" refine=3 lvl=90/90;
raiden add set="fopl" count=5;
raiden add stats hp=4780 atk=311 em=187 em=187 em=187; #mainstats
raiden add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1102 em=118.92 cr=0.3972 cd=0.5296; #substats

xingqiu char lvl=90/90 cons=6 talent=9,9,9;
xingqiu add weapon="favoniussword" refine=3 lvl=90/90;
xingqiu add set="eosf" count=5;
xingqiu add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cr=0.311; #mainstats
xingqiu add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944; #substats

nahida char lvl=90/90 cons=0 talent=9,9,9;
nahida add weapon="wanderingevenstar" refine=1 lvl=90/90;
nahida add set="deepwood" count=5;
nahida add stats hp=4780 atk=311 em=187 dendro%=0.466 em=187 ; #main
nahida add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.2204 em=138.74 cr=0.2979 cd=0.5296;

# --- Options
options iteration=1000;
energy every interval=480,720 amount=1;

# --- Targets
target lvl=100 resist=0.1 hp=999999999999 radius=2 pos=3,0;

# --- Rotation
active raiden;

raiden skill;
  
for let r = 0; r < 5; r = r + 1 { # End sim after 5 rotations

  while !.nahida.burst.ready {
    nahida attack;
  }
  nahida burst, skill;
  xingqiu skill, dash;
  
  while !.xingqiu.burst.ready {
    xingqiu attack;
  }
  
  xingqiu burst, attack;
  alhaitham skill[hold=1], low_plunge[short=1], 
            attack:2, charge, dash, 
            attack:3, charge, dash;
  raiden attack, skill, attack; 
  
  while !.alhaitham.burst.ready { # These burst conditionals just improve some unlucky iterations
    alhaitham attack;
  }
  
  alhaitham burst;
  nahida attack, skill; # Beware that Hold E is risky here
  alhaitham attack:3, jump,
            attack:3, jump, 
            attack:2, charge, jump,
            attack:3, dash, 
            attack:2;
}

sleep(52); # Until final mirror hitmark

# Raiden E > Nahida (N1) QE > Xingqiu EQ (N1) Q N1 > Alhaitham E lP N2CD N3CD
# Raiden N1 E N1 > Alhaitham (N1) Q > Nahida N1 E N1 > Alhaitham 2N3J N2CJ N3D N2

# Adds a random (normally distributed, mean 12f stddev 3f) delay to swaps
# Helps some configs average towards a result that is not an accidental byproduct of otherwise totally fixed frames.