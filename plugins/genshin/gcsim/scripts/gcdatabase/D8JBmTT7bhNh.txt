options swap_delay=12 iteration=500 duration=110;

kokomi char lvl=90/90 cons=0 talent=9,9,9; 
kokomi add weapon="prototypeamber" refine=5 lvl=90/90;
kokomi add set="oceanhuedclam" count=5;
kokomi add stats hp=4780 atk=311 hp%=0.466 hydro%=0.466 heal=0.359 ; #main
#kokomi add stats def%=0.124 def=39.36 hp=507.88 hp%=0.496 atk=33.08 atk%=0.3968 er=0.4408 em=39.64 cr=0.0662 cd=0.1324;
kokomi add stats def%=0.124 def=39.36 hp=507.88 hp%=0.496 atk=33.08 atk%=0.5952 er=0.2204 em=39.64 cr=0.0662 cd=0.1324;
#kokomi add stats def=39.36 def%=0.124 hp=2031.52 hp%=0.5952 atk=33.08 atk%=0.2976 er=0.1102 em=39.64 cr=0.0662 cd=0.1324;

yelan char lvl=90/90 cons=0 talent=9,9,9; 
yelan add weapon="favoniuswarbow" refine=3 lvl=90/90;
yelan add set="emblemofseveredfate" count=4;
yelan add stats hp=4780 atk=311 hp%=0.466 hydro%=0.466 cr=0.311; #main
yelan add stats hp%=0.1984 hp=507.88 atk%=0.0992 atk=33.08 def%=0.124 def=39.36 em=39.64 cr=0.331 cd=0.7944 er=0.1102;

target lvl=100 resist=.1 radius=2 pos=0,2.4;
energy every interval=480,720 amount=1;

active kokomi;

let third = .kokomi.cons >= 1;
let maxcombo = 10;
if third { maxcombo = 6; }

let last = f();
let i = 0;
for let x = 5; x || f() < last + 11*60 {
  switch {
  case .status.kokomiburst && i:
    kokomi attack:2;
    if third { kokomi attack; }
    if .stam > 20 { kokomi dash, walk[f=2]; } else { kokomi jump; }
    i = i - 1;
  case .kokomi.skill.ready && !.status.kokomiskill:
    if .status.yelanburst { kokomi attack; }
    kokomi skill;
  case .yelan.skill.ready:
    if .status.yelanburst { yelan attack; }
    yelan skill;
    if .yelan.energy < 70 - 4*2 { yelan attack:4; }
  case .yelan.burst.ready && .status.kokomiskill:
    yelan burst, attack;
  case .yelan.weapon == .keys.weapon.favoniuswarbow && !.yelan.mods.favonius-cd:
    yelan attack;
  case .kokomi.burst.ready && .status.kokomiskill >= 60 && .status.yelanburst:
    if .status.yelanburst { kokomi attack; }
    kokomi burst;
    last = f();
    i = maxcombo;
    x = x - 1;
  case .kokomi.onfield:
    kokomi attack;
  case .yelan.onfield:
    yelan attack;
  default:
    wait(1);
  }
}