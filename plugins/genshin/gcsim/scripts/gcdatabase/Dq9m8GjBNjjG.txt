options swap_delay=12 iteration=1000;

jean char lvl=90/90 cons=0 talent=9,9,9;
jean add set="shim" count=4;
# Use 4p BS, if you don't want the energy drain (for a small DPS loss)
#jean add set="blizzard" count=4;
jean add weapon="blacksword" refine=1 lvl=90/90;
jean add stats hp=4780 atk=311 atk%=0.466 cryo%=0.466 cd=0.622;
jean add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.3972 cd=0.662;

chongyun char lvl=90/90 cons=6 talent=9,9,9;
chongyun add weapon="serpent" refine=1 lvl=90/90 +params=[stacks=5];
chongyun add set="blizzard" count=4;
# Can wear 4p NO instead of Shenhe (switches Shenhe to 2p SR + 2p GF)
# Same team DPS, but higher stddev
#chongyun add set="noblesse" count=4;
chongyun add stats hp=4780 atk=311 atk%=0.466 cryo%=0.466 cd=0.622;
chongyun add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.3306 em=39.64 cr=0.331 cd=0.662;

kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="favsword" refine=3 lvl=90/90;
kazuha add set="viridescent" count=4;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187;
kazuha add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=118.92 cr=0.3972 cd=0.1324;

shenhe char lvl=90/90 cons=0 talent=9,9,9;
shenhe add weapon="favlance" refine=3 lvl=90/90;
shenhe add set="noblesse" count=4;
# Alternative if Chongyun wears 4p NO
#shenhe add set="shim" count=2;
#shenhe add set="glad" count=2;
shenhe add stats hp=4780 atk=311 atk%=0.466 atk%=0.466 cr=0.311;
shenhe add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

active shenhe;
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=999999999; 
energy every interval=480,720 amount=1;

for let i=0; i<4; i=i+1 {
  shenhe skill[hold=1];
  if .shenhe.burst.ready {
    shenhe burst;
  } else {
    shenhe attack;
  }

  kazuha skill[hold=1], high_plunge;
  # Forcing Kazuha's burst every rotation isn't worth the energy issues
  # Especially if Jean uses 4p BS and doesn't use her skill
  if .kazuha.burst.ready {
    kazuha burst;
  }

  chongyun skill, attack, burst;

  jean skill; # skip if 4p BS on Jean
  jean attack, charge;
  jean attack, charge;
  jean attack, charge;
  jean attack, charge;
  jean attack, charge;

  print("rotation done");
}

