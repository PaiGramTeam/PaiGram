# 12 frame swap delay
options swap_delay=12 iteration=1000;

sucrose char lvl=90/90 cons=6 talent=9,9,9;
sucrose add weapon="hakushinring" refine=5 lvl=90/90;
sucrose add set="viridescentvenerer" count=5;
sucrose add stats hp=4780 atk=311 em=187 em=187 em=187; # main 
sucrose add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.3968 er=0.6612 em=118.92 cr=0.0662 cd=0.1324; # sub

keqing char lvl=90/90 cons=0 talent=9,9,9;
keqing add weapon="harbingerofdawn" refine=5 lvl=90/90;
keqing add set="thunderingfury" count=5;
keqing add stats hp=4780 atk=311 atk%=0.466 electro%=0.466 cd=0.622; # main
keqing add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.3972 cd=0.662; # sub

fischl char lvl=90/90 cons=6 talent=9,9,9;
fischl add weapon="stringless" refine=3 lvl=90/90;
fischl add set="thundersoother" count=5;
fischl add stats hp=4780 atk=311 atk%=0.466 electro%=0.466 cr=0.311; # main
fischl add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944; # sub

collei char lvl=90/90 cons=6 talent=9,9,9;
collei add weapon="favoniuswarbow" refine=3 lvl=90/90;
collei add set="instructor" count=5;
collei add stats hp=3571 atk=232 atk%=0.348 dendro%=0.3492 cr=0.311; # main
collei add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=26.5 atk%=0.0794 er=0.1322 em=31.71 cr=0.2648 cd=0.58254;

energy every interval=480,720 amount=1; # Clear particle every 8-12 seconds
target lvl=100 resist=0.1 hp=10000000; # Impossibly high HP so target doesn't die but duration is ignored

active fischl;

# End simulation after 4 rotations
let rotations = 4;

while rotations {
  fischl burst;
  collei burst;
  sucrose skill, burst, attack;
  keqing skill, burst, skill, 
    attack, charge, 
    attack, charge, 
    attack, skill;
  # Waits are used to optimize Fischl A4 ICD
    wait(22);
  keqing attack, charge;
    wait(16);
  keqing attack, charge;
  fischl skill;
  # Collei N1 is used to proc Fischl C6 for VV and extra Favonius chance
  collei skill, attack;
  sucrose attack, skill, dash;
  keqing skill, burst, skill, 
    attack, charge, 
    attack, charge, 
    attack, skill;
    wait(22);
  keqing attack, charge;
    wait(16);
  # No final CA to end rotation at almost exactly 25s
  keqing attack;
  # Decrement rotation counter
  rotations = rotations - 1;
}

# Fischl Q > Collei Q > Sucrose EQ N1 > Keqing EQE 2N1C N1E 2N1C > Fischl E > Collei E N1 > Sucrose N1 ED > Keqing EQE 2N1C N1E N1C N1