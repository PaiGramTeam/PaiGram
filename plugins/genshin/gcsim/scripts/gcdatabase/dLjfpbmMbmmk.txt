
diluc char lvl=90/90 cons=0 talent=9,9,9; 
diluc add weapon="serpentspine" refine=1 lvl=90/90 +params=[stacks=5];
diluc add set="crimsonwitchofflames" count=4;
diluc add stats hp=4780 atk=311 em=187 pyro%=0.466 cd=0.622; #main
diluc add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1102 em=79.28 cr=0.3972 cd=0.662;

xingqiu char lvl=90/90 cons=6 talent=9,9,9; 
xingqiu add weapon="favoniussword" refine=5 lvl=90/90;
xingqiu add set="emblemofseveredfate" count=4;
xingqiu add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cr=0.311; #main
xingqiu add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1488 er=0.16503 em=39.64 cr=0.331 cd=0.7944;

fischl char lvl=90/90 cons=6 talent=9,9,9; 
fischl add weapon="stringless" refine=3 lvl=90/90;
fischl add set="gt" count=4;
fischl add stats hp=4780 atk=311 atk%=0.466 electro%=0.466 cr=0.311 ; #main
fischl add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

bennett char lvl=90/90 cons=6 talent=9,9,9; 
bennett add weapon="thealleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=5;
bennett add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311 ; 
bennett add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.3306 em=39.64 cr=0.2648 cd=0.7944;

options swap_delay=12 iteration=100; 
 
active xingqiu;
target lvl=100 resist=.1 radius=2 pos=0,2.4 hp=0999999999999;
energy every interval=480,720 amount=1;

for let x=0; x<4; x=x+1 {
  xingqiu burst, attack;
  bennett skill, attack, burst;
  if .fischl.burst.ready {
    fischl attack:2, burst;
  } else {
    fischl attack, skill;
  }
  diluc attack, skill, attack, skill, attack, skill, attack:2;
  xingqiu skill, dash, attack:2;
  diluc attack, burst, attack, skill, attack:2, skill, skill, attack;
}
