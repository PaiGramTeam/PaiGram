lyney char lvl=90/90 cons=0 talent=9,9,9;
lyney add weapon="prototypecrescent" refine=5 lvl=90/90;
lyney add set="marechausseehunter" count=4;
lyney add stats hp=4780 atk=311 atk%=0.466 pyro%=0.466 cd=0.622 ; #main
lyney add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.3972 cd=0.662;

zhongli char lvl=90/90 cons=0 talent=9,9,9; 
zhongli add weapon="favoniuslance" refine=3 lvl=90/90;
zhongli add set="archaicpetra" count=4;
zhongli add stats hp=4780 atk=311 atk%=0.466 geo%=0.466 cr=0.311 ; #main
zhongli add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="xiphos" refine=1 lvl=90/90;
kazuha add set="viridescentvenerer" count=5;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187 ; #main
kazuha add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.3857 em=118.92 cr=0.3641 cd=0.2648;

bennett char lvl=90/90 cons=6 talent=9,9,9;
bennett add weapon="alleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=4;
bennett add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311 ; #main
bennett add stats def=39.36 def%=0.124 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.551 em=39.64 cr=0.1986 cd=0.662;

options swap_delay=6 iteration=1000; # Swaps are 10f minimum (6f base with 2-6f delay before and after)

target lvl=100 resist=.1 hp=10000000 radius=2 pos=0,3;
energy every interval=480,720 amount=1;

let n = 2;
let m = 6;

# Random delay (uniform) between n and m frames, inclusive
fn rand_delay() {
  let del = rand() * (m - n + 1) + n;
  delay(del);
  print(del);
}

active lyney;

rand_delay();
lyney aim[weakspot=1];

for let r = 4; r > 0; r = r - 1 {
  rand_delay();
  zhongli swap;
  rand_delay();
  
  while !.zhongli.mods.favonius-cd && !.bennett.burst.ready {
      zhongli attack;
  }
  
  rand_delay();
  zhongli skill[hold=1];
  rand_delay();
  zhongli dash;
  rand_delay();
  
  bennett swap;
  rand_delay();
  bennett skill;
  rand_delay();
  bennett burst;
  rand_delay();
  kazuha swap;
  
  if .kazuha.burst.ready {
    rand_delay();
    kazuha burst;
  } else {
    rand_delay();
    kazuha skill;
    rand_delay();
    kazuha high_plunge;
  }
  
  rand_delay();
  lyney swap;
  rand_delay();
  lyney aim[weakspot=1];
  rand_delay();
  lyney aim;
  rand_delay();
  
  if .lyney.burst.ready {
    lyney burst;
    delay(11); # Long enough for burst firework to spawn
    rand_delay();
    lyney skill;
    rand_delay();
  } else {
    lyney aim;
    rand_delay();
  }
  
  lyney aim;
  rand_delay();
  lyney skill;
  rand_delay();
  lyney aim;
}
sleep(9); # Until final aim hitmark

# Lyney CS > Zhongli hED > Bennett EQ > Kazuha Q if ready, otherwise E hP > Lyney 2CS > Lyney Qc if ready, otherwise CS > Lyney CS E CS
#          > Loop at Zhongli