klee char lvl=90/90 cons=0 talent=9,9,9;
klee add weapon="widsith" refine=3 lvl=90/90;
klee add set="crimson" count=4;
klee add stats hp=4780 atk=311 em=187 pyro%=0.466 cr=0.311 ; #main
klee add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;
#main w/ instructors
# klee add stats hp=4780 atk=311 atk%=0.466 pyro%=0.466 cr=0.311 ; #main

xingqiu char lvl=90/90 cons=6 talent=9,9,9;
xingqiu add weapon="favsword" refine=3 lvl=90/90;
xingqiu add set="eosf" count=4;
xingqiu add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cr=0.311; #main
xingqiu add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.2204 em=39.64 cr=0.331 cd=0.7944;
# noblesse xq (same main)
# xingqiu add set="no" count=4;
# xingqiu add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=39.64 cr=0.331 cd=0.5296;


bennett char lvl=90/90 cons=6 talent=9,9,9;
bennett add weapon="alleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=4; 
bennett add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311 ; #main
bennett add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=237.84 cr=0.1324 cd=0.2648;
# instructors bennett
# bennett add set="instructor" count=4;
# bennett add stats hp=3571 atk=232 er=0.518 pyro%=0.3495 cr=0.23325; # main
# bennett add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=158.56 cr=0.0662 cd=0.1324; 
# 0.84x scalar (8 er, 6 cr, 6 cd) to account for one 5* artifact

kazuha char lvl=90/90 cons=0 talent=9,9,9; 
kazuha add weapon="favoniussword" refine=3 lvl=90/90;
kazuha add set="viridescentvenerer" count=4;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187 ; #main
kazuha add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.4408 em=118.92 cr=0.3972 cd=0.1324;
	

options swap_delay=12 iteration=1000;
target lvl=100 resist=.1 hp=10000000 radius=2 pos=0,2.4;
energy every interval=480,720 amount=1;

active klee;
klee skill[bounce=3, mines=8];

for let r = 0; r < 5; r = r + 1 {
  xingqiu burst, attack, skill, dash, attack:2;
  kazuha attack, burst;
  bennett attack, skill, burst, attack;
  kazuha skill[hold=1], high_plunge, attack;
  klee attack;
  klee skill[bounce=3, mines=8];
  if .klee.burst.ready {
    klee burst;
  }
  for let r = 0; r <= 6; r = r + 1 {
    klee attack, charge;
  }
}

wait(25); # So sim doesn't end with final CA mid animation

# C0 Klee VV Vape  -- 

# Klee E >
# Xingqiu Q N1 E d N2 > Kazuha N1 Q > Bennett N1 Q E N1 > Kazuha hE hP n1 > Klee N1 E (Q) 6N1C
# Start again from Xingqiu.
