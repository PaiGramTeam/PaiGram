klee char lvl=90/90 cons=0 talent=9,9,9;
klee add weapon="favcodex" refine=3 lvl=90/90;
klee add set="crimson" count=4;
klee add stats hp=4780 atk=311 atk%=0.466 pyro%=0.466 cr=0.311 ; #main
klee add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

fischl char lvl=90/90 cons=6 talent=9,9,9;
fischl add weapon="stringless" refine=3 lvl=90/90;
fischl add set="gt" count=4;
fischl add stats hp=4780 atk=311 atk%=0.466 electro%=0.466 cr=0.311; 
fischl add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.1102 em=79.28 cr=0.331 cd=0.7944;

beidou char lvl=90/90 cons=6 talent=9,9,9;
beidou add weapon="serpentspine" refine=1 lvl=90/90 +params=[stack=5];
beidou add set="eosf" count=4;
beidou add stats hp=4780 atk=311 er=0.518 electro%=0.466 cd=0.622; 
beidou add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.3972 cd=0.662;

bennett char lvl=90/90 cons=6 talent=9,9,9;
bennett add weapon="thealleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=4;
bennett add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311 ; #main
bennett add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944;

options swap_delay=12 iteration=1000;
target lvl=100 resist=0.1 radius=2 pos=2.1,2.4 hp=999999999; 
target lvl=100 resist=0.1 radius=2 pos=-2.1,2.4 hp=999999999; 
energy every interval=480,720 amount=1;

active bennett;

let normal_animation = 1;
let charge_animation = 2;

for let i=0; i<4; i=i+1 {
  let src = f();
  let counter_chance = rand();
  let funnel = 0;
  
  bennett skill, burst;
  
  if .fischl.burst.ready {
    fischl attack:2, burst;
  } else {
    fischl skill, attack;
  }
  
  # 50% chance to counter, 10% chance to perfect counter
  if counter_chance <= 0.1 {
    beidou skill[counter=2];
  } else if counter_chance <= 0.5 {
    beidou skill[counter=1];
  } else {
    beidou skill;
  }
  
  beidou burst;
  
  klee attack, burst, walk;
  
  while f() - src <= 20 * 60 {
    if .klee.skill.ready {
      klee attack, skill[bounce=3, mine=8], jump;
    } else if .klee.onfield && .state == normal_animation && ( .stam >= 50 || .status.kleespark ) {
      klee charge;
    } else if .klee.onfield && .klee.normal > 1 && .state == normal_animation {
      klee walk;
    } else if !.status.kleeq && .bennett.skill.ready && !.status.bennettburst && !funnel && ( .bennett.energy < 50 || .klee.energy < 50 ) {
      bennett skill, attack:2;
      funnel = 1;
    } else {
      klee attack;
    }
  }
  
  # Don't end rotation with uncanceled CA
  if .klee.onfield && .state == charge_animation {
    klee attack;
  }
}

# Favonius Klee Overload with Fischl+Beidou -- Pseudo APL rotation
# Bennett EQ > Fischl N2Q / EN1 > Beidou EQ > Klee N1 Q > (Klee N1EJ / N1CJ / N1W spam until 20s, Bennett funnel his own particles once if needed)
